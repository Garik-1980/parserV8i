Функция ЗаписатьРезультатВФайл(ИмяФайла,Данные)
  Текст = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.ANSI); 
  Текст.Записать(Данные); 
  Текст.Закрыть();    
КонецФункции // ЗаписатьРезультатВФайл(ИмяФайла,Данные)

Функция ТекстИниФайла(ИниФайл)
  Если Не ИниФайл.Существует() Тогда
    ВызватьИсключение "Не найден ини файл "+ИниФайл.ПолноеИмя;
  КонецЕсли;
  ТекстИни = Новый ЧтениеТекста(ИниФайл.ПолноеИмя, "utf-8");
  Текст = ТекстИни.Прочитать();
  Если ПустаяСтрока(Текст) Тогда
    ВызватьИсключение "Из ини файла ничего не прочитано"
  Иначе
    Возврат Текст;
  КонецЕсли;
КонецФункции

Функция ПолучитьСписокБаз(ИмяИНИФайла) Экспорт
  
  Данные = ТекстИниФайла(Новый Файл(ИмяИНИФайла));
  РегВыражениеСписокБаз = новый РегулярноеВыражение("\[[^\]\r\n]+](?:\r?\n(?:[^\[\r\n].*)?)*");
  РегВыражениеПереносСтрок = новый РегулярноеВыражение("\r\n");
  РегВыражениеЗначения = новый РегулярноеВыражение("(=|;)(?=(?:[^\""]*\""[^\""]*\"")*(?![^\""]*\""))");

  Базы = РегВыражениеСписокБаз.НайтиСовпадения(Данные);
  
  СписокБаз = Новый Соответствие;
  Для каждого База из Базы Цикл
    МассивСтр = РегВыражениеПереносСтрок.Разделить(База.Значение);
    СтруктураПути = Новый  Структура();
    Для каждого Стр из МассивСтр Цикл
      Если СокрЛП(Стр) = "" Тогда
        Продолжить;
      КонецЕсли;
      Если (Лев(Стр, 1) = "[") Тогда
        СтруктураПути.Вставить("Name",Сред(Стр,2,СтрДлина(Стр)-2));
      ИначеЕсли ВРег(Лев(Стр, 7)) = ВРег("Connect") Тогда
        СтрАдрес = Сред(Стр,9);
        РезАдрес  = РегВыражениеЗначения.Разделить(СтрАдрес);
        СтруктураСоединения = Новый Структура();
        Если РезАдрес.Количество() = 5 Тогда
          СтруктураСоединения.Вставить(РезАдрес[0],РезАдрес[2]);
        ИначеЕсли РезАдрес.Количество() = 9 Тогда
          СтруктураСоединения.Вставить(РезАдрес[0],РезАдрес[2]);
          СтруктураСоединения.Вставить(РезАдрес[4],РезАдрес[6]);
        КонецЕсли;
        
        СтруктураАдреса = Новый Структура;
        СтруктураАдреса.Вставить("String",Стр);
        СтруктураАдреса.Вставить("Structure",СтруктураСоединения);
        СтруктураПути.Вставить("Connect",СтруктураАдреса);
      Иначе
        ПозицияРавно = СтрНайти(Стр,"=");
        СтруктураПути.Вставить(Сред(Стр,1,ПозицияРавно-1),Сред(Стр,ПозицияРавно+1));
      КонецЕсли;
      
    КонецЦикла;
    СписокБаз.Вставить(СтруктураПути.Name,СтруктураПути);
  КонецЦикла;

  Возврат СписокБаз;
КонецФункции

Функция ЗаписатьСписокБаз(ИмяИНИФайла,СписокБаз) Экспорт
  
  Данные = "";
  Для каждого База Из СписокБаз Цикл
    СтруктураАдреса = База.Значение;

    Для каждого Часть Из СтруктураАдреса Цикл
      Если Часть.Ключ = "Name" Тогда
       Данные = Данные + Символы.ВК + Символы.ПС + "[" + Часть.Значение + "]";
      ИначеЕсли Часть.Ключ = "Connect" Тогда
       Данные = Данные + Символы.ВК + Символы.ПС + Часть.Значение.String;
      Иначе
        Данные = Данные + Символы.ВК + Символы.ПС + Часть.Ключ + "=" + Часть.Значение;
      КонецЕсли;
    КонецЦикла;
    
  КонецЦикла;

  ЗаписатьРезультатВФайл(ИмяИНИФайла,СокрЛП(Данные));

КонецФункции // ЗаписатьСписокБаз()

Функция ПолучитьСекциюФайлаПоИБ(ИмяИНИФайла, ИБимя) Экспорт
  СписокБаз = ПолучитьСписокБаз(ИмяИНИФайла);
  Возврат СписокБаз[ИБимя];
КонецФункции

//////////////////////////// ***** /////////////////////////////
иб = "tasks";

СИ = Новый СистемнаяИнформация;
Енв = СИ.ПеременныеСреды();

кнф = Енв.Получить("USERPROFILE")+"\appdata\roaming\1c\1cestart\ibases.v8i";
СписокБаз = ПолучитьСписокБаз(кнф);

ЗаписатьСписокБаз(кнф+".new",СписокБаз);

БазаЗадачи = ПолучитьСекциюФайлаПоИБ(кнф,иб);
юзр = Енв.Получить("USERNAME");
кеш = Енв.Получить("USERPROFILE")+"\appdata\local\1c\1cv8\" + БазаЗадачи.ID;
Сообщить("Для пользователя "+ юзр +" ИБ "+БазаЗадачи.Name+" каталог кеша "+ кеш + ". Path: " + БазаЗадачи.Connect.String);
ФКеш = Новый Файл(кеш);
Если Не ФКеш.Существует() Тогда
  Сообщить("Каталог кеша не обнаружен");
КонецЕсли;
