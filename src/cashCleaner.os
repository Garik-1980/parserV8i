Перем Парсер;

Функция УдалитьКаталог(ПутьКаталога)
	Попытка
		УдалитьФайлы(ПутьКаталога);	
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;
КонецФункции // УдалитьКешБазы(База)

///  Очищает кеш всех баз из файла ibases.v8i
//
// Параметры:
//  ПутьКФайлуСпискаБаз - Строка - Путь к файлу, если не задан берет файл по умолчанию

Функция ОчиститьВесьКеш(ПутьКФайлуСпискаБаз="") Экспорт
	
	СИ = Новый СистемнаяИнформация;
	Окружение = СИ.ПеременныеСреды();

	Если ПутьКФайлуСпискаБаз = "" Тогда
		ПутьКФайлуСпискаБаз = Окружение.Получить("USERPROFILE")+"\appdata\roaming\1c\1cestart\ibases.v8i";
	КонецЕсли;

	СписокБаз = УправлениеСписком.ПолучитьСписокБаз(ПутьКФайлуСпискаБаз);

	Для каждого Стр Из СписокБаз Цикл
		База = Стр.Значение;

		Сообщить("Очистка кеша базы: " + База.Name);
		КаталогКеша = Окружение.Получить("USERPROFILE")+"\appdata\local\1c\1cv8\" + База.ID;
		УдалитьКаталог(КаталогКеша);

	КонецЦикла;

	Возврат Истина;
КонецФункции // ОчиститьВесьКеш()

///  Очищает кеш базы
//
// Параметры:
//  Параметры      - Структура - Ключ: "Путь", Значение: Путь к БД;
//
// Возвращаемое значение:
//  Булево - Очистка прошла успешно/не успешно
// 
Функция ОчиститьКешБазы(Параметры) Экспорт
	СИ = Новый СистемнаяИнформация;
	Окружение = СИ.ПеременныеСреды();
	
	ПутьКФайлуСпискаБаз = "";
	Если Параметры.Свойство("Файл") Тогда
		ПутьКФайлуСпискаБаз = Параметры.Файл;
	КонецЕсли;

	Если СокрЛП(ПутьКФайлуСпискаБаз) = "" Тогда
		ПутьКФайлуСпискаБаз = Окружение.Получить("USERPROFILE")+"\appdata\roaming\1c\1cestart\ibases.v8i";
	КонецЕсли;
	
	СписокБаз = УправлениеСписком.ПолучитьСписокБаз(ПутьКФайлуСпискаБаз);
	ПутьОчистки = "";
	Для каждого Стр Из СписокБаз Цикл
		База = Стр.Значение;

		Если База.Свойство("Connect") Тогда
			СтрБ = База.Connect.Structure;
			Если СтрБ.Свойство("File") Тогда
				Если ВРЕГ(СтрБ.File) = ВРег(Параметры.Путь) Тогда
					ПутьОчистки = Окружение.Получить("USERPROFILE")+"\appdata\local\1c\1cv8\" + База.ID;
					Прервать;
				КонецЕсли;
			ИначеЕсли СтрБ.Свойство("ws") Тогда
				Если ВРЕГ(СтрБ.ws) = ВРег(Параметры.Путь) Тогда
					ПутьОчистки = Окружение.Получить("USERPROFILE")+"\appdata\local\1c\1cv8\" + База.ID;
					Прервать;
				КонецЕсли;
			ИначеЕсли СтрБ.Свойство("Srvr") Тогда
				Если ВРЕГ(СтрБ.Srvr) = ВРег(Параметры.Путь) Тогда
					ПутьОчистки = Окружение.Получить("USERPROFILE")+"\appdata\local\1c\1cv8\" + База.ID;
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

	Если СокрЛП(ПутьОчистки) <> "" Тогда
		УдалитьКаталог(ПутьОчистки);
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;
КонецФункции

Процедура УстановитьПарсер(Источник) Экспорт
	Парсер = Источник;
КонецПроцедуры

Функция ПолучитьПарсер() Экспорт
	Возврат Парсер;
КонецФункции
